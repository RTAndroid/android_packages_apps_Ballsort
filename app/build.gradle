import org.gradle.internal.os.OperatingSystem;

apply plugin: 'com.android.application'

def getNdkExecutable() {
    def ndkDir = android.ndkDirectory.getAbsolutePath()
    if (OperatingSystem.current().isWindows()) {
        "$ndkDir/ndk-build.cmd"
    }
    else {
        "$ndkDir/ndk-build"
    }
}

task buildNative(type: Exec) {
    commandLine getNdkExecutable(),
            '-C', file('src/main/jni').absolutePath,
            'APP_PLATFORM=android-22'
}

task cleanNative(type: Exec) {
    commandLine getNdkExecutable(),
            '-C', file('src/main/jni').absolutePath,
            'clean'
}

android {
    compileSdkVersion 22
    buildToolsVersion '24.0.2'

    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "rtandroid.ballsort"
        minSdkVersion 22
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        jackOptions {
            enabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ['src/main/libs']
        }

        tasks.withType(JavaCompile) { compileTask -> compileTask.dependsOn buildNative }
        clean.dependsOn cleanNative
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:design:22.2.0'
    compile files('src/main/libs/neuroph-core-2.92.jar')
    compile files('src/main/libs/slf4j-android-1.7.21.jar')
    compile files('src/main/libs/slf4j-api-1.7.21.jar')
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    provided fileTree(include: 'rtandroid-20160721.jar', dir: 'api')
}
